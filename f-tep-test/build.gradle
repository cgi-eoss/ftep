group 'com.cgi.eoss.f-tep'
version '2.1.0'

apply plugin: 'java'
apply plugin: 'jacoco'

String harnessDir = "${buildDir}/harness"

// !! WARNING !!
//
// This module is testing the end result of the full distribution build and
// should not be run until after the .dist directory is fully populated.
//
// The acceptance tests may be enabled by passing the property on the command
// line, e.g. "gradle -PacceptanceTests check".
test {
    onlyIf {
        project.hasProperty("acceptanceTests")
    }

    systemProperties['HARNESS_DIR'] = harnessDir

    testLogging.showStandardStreams = true

    ignoreFailures Boolean.getBoolean("test.ignoreFailures")
}

if (project.hasProperty("acceptanceTests")) {
    test.dependsOn 'copyFtepDist'
}

task copyFtepDist(type: Sync) {
    into harnessDir
    from "${projectDir}/src/test/docker"
    from "${projectDir}/../.dist"
}

dependencies {
    testCompileOnly 'org.projectlombok:lombok'

    testCompile 'com.squareup.okhttp3:okhttp'
    testCompile 'info.cukes:cucumber-java8'
    testCompile 'info.cukes:cucumber-junit'
    testCompile 'info.cukes:cucumber-picocontainer'
    testCompile 'junit:junit'
    testCompile 'org.awaitility:awaitility'
    testCompile 'org.seleniumhq.selenium:selenium-java'
    testCompile 'org.testcontainers:selenium'
    testCompile 'org.testcontainers:testcontainers'
    testCompile 'org.springframework.boot:spring-boot-starter-log4j2'
}
