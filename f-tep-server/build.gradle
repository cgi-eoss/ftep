buildscript {
    dependencies {
        classpath pl.osPackage
        classpath pl.springBoot
    }
}

group 'com.cgi.eoss.f-tep'
version '0.4.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'nebula.ospackage'

dependencies {
    compile project(':f-tep-api')
    compile project(':f-tep-orchestrator')

    compile deps.springContext
    compile deps.springBootAutoconfigure

    runtime deps.javaxServletApi
    runtime deps.logbackClassic
    runtime deps.springBootUndertow
    runtime deps.springDataRestHalBrowser
    runtime deps.aspectjWeaver

    // Default provided database adapters
    runtime deps.hsqldb
    runtime deps.postgresql
}

springBoot {
    executable = true
}

// bootRepackage was depending on "archives" and creating a circular dependency
bootRepackage.dependsOn = [jar]
buildRpm.dependsOn bootRepackage
buildDeb.dependsOn bootRepackage
artifacts { archives buildRpm }

ospackage {
    packageName 'f-tep-server'
    version getRpmVersion(project.version).version
    release getRpmVersion(project.version).release
    arch NOARCH
    os LINUX

    user 'ftep'
    permissionGroup 'ftep'

    preInstall file('src/ospackage/preinst.sh')

    into '/var/f-tep'

    from("${buildDir}/libs") {
        into 'bin'
        include '*.jar'
        rename('f-tep-server-.*\\.jar', 'f-tep-server.jar')
    }

    from("src/ospackage/f-tep-server.properties") {
        into 'etc'
        fileType CONFIG | NOREPLACE
    }

    link('/etc/init.d/f-tep-server', '/var/f-tep/bin/f-tep-server.jar')
}
