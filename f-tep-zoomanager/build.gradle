buildscript {
    dependencies {
        classpath pl.osDetector
        classpath pl.osPackage
        classpath pl.springBoot
    }
}

group 'com.cgi.eoss.f-tep'
version '1.4.0'

apply plugin: 'java'
apply plugin: 'jacoco'

apply plugin: 'org.springframework.boot'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.google.osdetector'

configurations {
    zooJars
}

dependencies {
    compileOnly 'org.projectlombok:lombok'

    compile project(':f-tep-model')
    compile project(':f-tep-rpc')
    compile project(':f-tep-zoolib')

    compile 'com.google.guava:guava'
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    compile 'com.lmax:disruptor'
    compile 'io.netty:netty-tcnative-boringssl-static'
    compile 'org.apache.logging.log4j:log4j-iostreams'
    compile 'org.apache.logging.log4j:log4j-jul'
    compile 'org.freemarker:freemarker'
    compile 'org.graylog2.log4j2:log4j2-gelf'
    compile 'org.jolokia:jolokia-core'
    compile 'org.jooq:jool'
    compile 'org.lognet:grpc-spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-log4j2'
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'

    testCompile 'org.hamcrest:hamcrest-junit'
    testCompile 'com.google.jimfs:jimfs'
    testCompile 'junit:junit'
    testCompile 'org.mockito:mockito-core'
    testCompile 'org.springframework:spring-test'
    testCompile 'org.springframework.boot:spring-boot-test'

    zooJars(project(path: ':f-tep-zoolib', configuration: 'shadow')) { transitive = false }
}

springBoot {
    classifier = 'bin'
    executable = true
}

// bootRepackage was depending on "archives" and creating a circular dependency
bootRepackage.dependsOn = [jar]
buildRpm.dependsOn bootRepackage
buildDeb.dependsOn bootRepackage
configurations { pkg }
artifacts { pkg buildRpm }

ospackage {
    packageName 'f-tep-zoomanager'
    version getRpmVersion(project.version).version
    release getRpmVersion(project.version).release
    arch NOARCH
    os LINUX

    user 'ftep'
    permissionGroup 'ftep'

    preInstall file('src/ospackage/preinst.sh')
    postInstall file('src/ospackage/postinst.sh')

    into('/var/f-tep/zoomanager') {
        from("${buildDir}/libs") {
            include '*-bin.jar'
            rename('f-tep-zoomanager-.*\\-bin.jar', 'f-tep-zoomanager.jar')
            fileMode 0500
        }

        from("src/ospackage/application.properties") {
            fileType CONFIG | NOREPLACE
        }
    }

    into('/var/www/cgi-bin/jars') {
        from configurations.zooJars
        rename('f-tep-zoolib-.*\\-all.jar', 'f-tep-zoolib.jar')
    }

    link('/etc/init.d/f-tep-zoomanager', '/var/f-tep/zoomanager/f-tep-zoomanager.jar')
}
