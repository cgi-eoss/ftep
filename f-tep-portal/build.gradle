buildscript {
    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:4.1.0"
        classpath "gradle.plugin.com.craigburke.gradle:client-dependencies:1.3.1"
    }
}

group 'com.cgi.eoss.f-tep'
version '0.2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.craigburke.client-dependencies'

jar {
    enabled false
}

clientDependencies {
    // Use a custom repo to allow local override of npmUrl
    registry 'npmR', type: 'npm', url: npmUrl

    npmR {
        'angular'('1.4.9')
        'bootstrap'('3.2.0')
        'jquery'('3.1.1')
        'angular-animate'('1.4.9')
        'angular-cookies'('1.4.9')
        'angular-resource'('1.4.9')
        'angular-route'('1.4.9')
        'angular-sanitize'('1.4.9')
        'angular-touch'('1.4.9')
        'angular-ui-grid'('3.2.9')
        'angular-ui-layout'('1.4.2')
        'angular-ui-bootstrap'('2.2.0')
        'angular-material'('1.1.1')
        'angularjs-slider'('5.8.7')
        'angular-aria'('1.5.8')
        'angular-messages'('1.5.8')
        'angular-openlayers-directive'('1.15.2')
        'angular-drag-and-drop-lists'('1.4.0')
        'angular-paging'('2.2.2')
        'requirejs'('2.3.1')
        'requirejs-domready'('2.0.1', into: 'domready')
        'clipboard-js'('0.3.1')
    }
}

buildRpm.dependsOn clientInstall
buildDeb.dependsOn clientInstall

ospackage {
    packageName 'f-tep-portal'
    version getRpmVersion(project.version).version
    release getRpmVersion(project.version).release
    arch NOARCH
    os LINUX

    into '/var/www/html/f-tep'

    from('src/main/resources') {
        user 'root'
        include 'app/**'
        exclude 'app/scripts/ftepConfig.js'
    }

    from('src/main/resources') {
        user 'root'
        fileType CONFIG | NOREPLACE
        include 'app/scripts/ftepConfig.js'
    }

    from('src/assets/vendor') {
        user 'root'
        into 'bower_components'
    }

    link('/var/www/html/f-tep/app/bower_components', '../bower_components')
}

artifacts {
    archives buildRpm
}
