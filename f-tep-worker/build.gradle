buildscript {
    dependencies {
        classpath pl.osPackage
        classpath pl.springBoot
    }
}

group 'com.cgi.eoss.f-tep'
version '0.6.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'nebula.ospackage'

dependencies {
    compileOnly 'org.projectlombok:lombok'

    compile project(':f-tep-rpc')

    compile 'ch.qos.logback:logback-classic'
    compile 'commons-io:commons-io'
    compile 'commons-net:commons-net'
    compile 'com.github.docker-java:docker-java'
    compile 'com.squareup.okhttp3:okhttp'
    compile 'javax.servlet:javax.servlet-api'
    compile 'org.aspectj:aspectjweaver'
    compile 'org.lognet:grpc-spring-boot-starter'
    compile 'org.slf4j:slf4j-api'
    compile 'org.springframework:spring-context'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-undertow'

    testCompile project(':f-tep-persistence')

    testCompile 'com.google.jimfs:jimfs'
    testCompile 'com.squareup.okhttp3:mockwebserver'
    testCompile 'junit:junit'
    testCompile 'org.hamcrest:hamcrest-junit'
    testCompile 'org.hsqldb:hsqldb'
    testCompile 'org.mockftpserver:MockFtpServer'
    testCompile 'org.mockito:mockito-core'
    testCompile 'org.springframework:spring-test'
}

springBoot {
    classifier = 'bin'
    executable = true
}

// bootRepackage was depending on "archives" and creating a circular dependency
bootRepackage.dependsOn = [jar]
buildRpm.dependsOn bootRepackage
buildDeb.dependsOn bootRepackage
configurations { pkg }
artifacts { pkg buildRpm }

ospackage {
    packageName 'f-tep-worker'
    version getRpmVersion(project.version).version
    release getRpmVersion(project.version).release
    arch NOARCH
    os LINUX

    user 'ftep'
    permissionGroup 'ftep'

    preInstall file('src/ospackage/preinst.sh')
    postInstall file('src/ospackage/postinst.sh')

    into '/var/f-tep'

    from("${buildDir}/libs") {
        into 'bin'
        include '*-bin.jar'
        rename('f-tep-worker-.*\\-bin.jar', 'f-tep-worker.jar')
        fileMode 0500
    }

    from("src/ospackage/f-tep-worker.properties") {
        into 'etc'
        fileType CONFIG | NOREPLACE
    }

    link('/etc/init.d/f-tep-worker', '/var/f-tep/bin/f-tep-worker.jar')
}
