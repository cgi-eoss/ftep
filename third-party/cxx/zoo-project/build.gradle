buildscript {
    dependencies {
        classpath pl.osPackage
    }
}

group 'com.cgi.eoss.f-tep'
version '1.6.0'

apply plugin: 'base'
apply plugin: 'nebula.ospackage'

task buildZoo(type: Exec) {
    executable "${projectDir}/build.sh"
}

ospackage {
    packageName 'zoo-kernel'
    version '1.6.0'
    release '1'
    arch X86_64
    os LINUX
    packageDescription 'ZOO-Kernel WPS server'
    url 'http://zoo-project.org/'
    license 'MIT License'
    vendor 'F-TEP'

    requires('java-1.8.0-openjdk-headless')
    requires('gdal')
    requires('fcgi')

    postInstall file('postinst.sh')

    into('/usr') {
        into('include/zoo') {
            from "${buildDir}/staging/usr/include/zoo"
            addParentDirs false
        }
        into('lib') {
            from "${buildDir}/staging/usr/lib"
        }
    }

    into('/var/www/cgi-bin') {
        from "${buildDir}/staging/var/www/cgi-bin"
        addParentDirs false

        into('sql') {
            from('src/zoo-project/zoo-kernel/sql/schema.sql') {
                rename('schema.sql', 'V1_6_0__Schema_install.sql')
                addParentDirs false
            }
        }
    }
}

buildRpm.dependsOn buildZoo
buildDeb.dependsOn buildZoo

sonarqube {
    skipProject = true
}
