buildscript {
    dependencies {
        classpath pl.osDetector
        classpath pl.osPackage
        classpath pl.springBoot
    }
}

group 'com.cgi.eoss.f-tep'
version '2.3.0'

apply plugin: 'java'
apply plugin: 'jacoco'

apply plugin: 'org.springframework.boot'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.google.osdetector'

dependencies {
    compile project(':f-tep-api')
    compile project(':f-tep-orchestrator')

    compile 'com.lmax:disruptor'
    compile 'io.netty:netty-tcnative-boringssl-static'
    compile 'javax.servlet:javax.servlet-api'
    compile 'org.apache.logging.log4j:log4j-jul'
    compile 'org.aspectj:aspectjweaver'
    compile 'org.graylog2.log4j2:log4j2-gelf'
    compile 'org.jolokia:jolokia-core'
    compile 'org.springframework:spring-context'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-log4j2'
    compile 'org.springframework.boot:spring-boot-starter-undertow'

    // Default provided database adapters
    runtime 'org.hsqldb:hsqldb'
    runtime 'org.postgresql:postgresql'

    testCompile project(':f-tep-worker')
    testCompile project(path: ':docker-java', configuration: 'shadow')
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

springBoot {
    classifier = 'bin'
    executable = true
}

// bootRepackage was depending on "archives" and creating a circular dependency
bootRepackage.dependsOn = [jar]
buildRpm.dependsOn bootRepackage
buildDeb.dependsOn bootRepackage
configurations { pkg }
artifacts { pkg buildRpm }

ospackage {
    packageName 'f-tep-server'
    version getRpmVersion(project.version).version
    release getRpmVersion(project.version).release
    arch NOARCH
    os LINUX

    user 'ftep'
    permissionGroup 'ftep'

    preInstall file('src/ospackage/preinst.sh')
    postInstall file('src/ospackage/postinst.sh')
    preUninstall file('src/ospackage/prerm.sh')

    into('/var/f-tep/server') {
        from("${buildDir}/libs") {
            include '*-bin.jar'
            rename('f-tep-server-.*\\-bin.jar', 'f-tep-server.jar')
            fileMode 0500
        }
        from('src/ospackage/application.properties') {
            fileType CONFIG | NOREPLACE
        }
    }

    into('/usr/lib/systemd/system') {
        from 'src/ospackage/f-tep-server.service'
        addParentDirs false
    }

    link('/etc/init.d/f-tep-server', '/var/f-tep/server/f-tep-server.jar')
}
